#
# Copyright (C) 2013 - 2019 Wind River Systems, Inc.
#
# Explicitly disallow these recipes when this layer is included.  Items are also blacklisted
# in .inc and .conf files associated with specific wrlinux distro's.  Use "?=" or "??=" in these
# files so that blacklisting can be overridden in local.conf.

# SKIP_RECIPE[PN] = "reason"

# Remove blacklist once the build appliance is supported.
SKIP_RECIPE[build-appliance-image] ?= "The build appliance image is not supported at this time."

SKIP_RECIPE[ajenti] ?= "The ajenti recipe does not have various required python modules available, this recipe is not supported."
SKIP_RECIPE[qt-mobility-x11] ?= "The qt-mobility-x11 recipe is not supported at this time."
SKIP_RECIPE[qt-mobility-embedded] ?= "The qt-mobility-embedded recipe is not supported at this time."

BL_SYSTEMTAP_MSG = ""
BL_SYSTEMTAP_MSG:osv-wrlinux = "Unsupported: perf is the preferred tool for system-wide debugging"
SKIP_RECIPE[systemtap] ?= "${BL_SYSTEMTAP_MSG}"
SKIP_RECIPE[systemtap-uprobes] ?= "${BL_SYSTEMTAP_MSG}"

# Prevent an 'unexpected' kernel from building...
KERNEL_NAME_SKIPLIST    = "${@'Only ${PREFERRED_PROVIDER_virtual/kernel} is supported in this configuration. ' if d.getVar('PREFERRED_PROVIDER_virtual/kernel') and (d.getVar('PREFERRED_PROVIDER_virtual/kernel') != d.getVar('PN')) else ''}"
KERNEL_VERSION_SKIPLIST = "${@'Only version ' + d.getVar('PREFERRED_VERSION_${BPN}') + ' (not ' + d.getVar('PV') + ') is supported in this configuration. ' if d.getVar('PREFERRED_VERSION_${BPN}') and not d.getVar('PV').startswith(d.getVar('PREFERRED_VERSION_${BPN}').replace('%', '')) else ''}"
SKIP_RECIPE[linux-yocto]      ?= "${KERNEL_NAME_SKIPLIST}${KERNEL_VERSION_SKIPLIST}"
SKIP_RECIPE[linux-yocto-rt]   ?= "${KERNEL_NAME_SKIPLIST}${KERNEL_VERSION_SKIPLIST}"
SKIP_RECIPE[linux-yocto-tiny] ?= "${KERNEL_NAME_SKIPLIST}${KERNEL_VERSION_SKIPLIST}"
SKIP_RECIPE[linux-yocto-dev]  ?= "${KERNEL_NAME_SKIPLIST}${KERNEL_VERSION_SKIPLIST}"

SKIP_RECIPE[linux-intel] ?= "The linux-intel kernel is not supported, use linux-yocto instead."
SKIP_RECIPE[linux-intel-rt] ?= "The linux-intel-rt kernel is not supported, use linux-yocto-rt instead."

BL_OPROFILE_MSG = ""
BL_OPROFILE_MSG:osv-wrlinux = "Unsupported in wrlinux distros."
SKIP_RECIPE[oprofile] ?= "${BL_OPROFILE_MSG}"

# Recipes below are blacklisted temporarily

SKIP_RECIPE[vc-intrinsics]  ?= "LINCD-5886 do_compile failed since llvm api changed"
SKIP_RECIPE[intel-graphics-compiler]  ?= "LINCD-5886 do_compile failed since llvm api changed"
SKIP_RECIPE[intel-compute-runtime]  ?= "LINCD-5886 blacklisted because depend on intel-graphics-compiler"
