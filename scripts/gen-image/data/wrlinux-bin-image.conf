TMPDIR = "${TOPDIR}/tmp-${MACHINE}"

DISTRO = "wrlinux-graphics"
PACKAGE_CLASSES = "package_rpm"
PRSERV_HOST = "localhost:0"

# Disable multilib
MULTILIBS = ""

# Make /var/log save on persistent storage to make the image work for container
VOLATILE_DIR = "0"

# For feature/passwd-expire
FORCE_EXPIRED_USERS = 'root'

INHERIT += "rm_work"
RM_WORK_EXCLUDE += "wrlinux-image-full wrlinux-image-minimal"

# Templates
WRTEMPLATE = "feature/docker feature/kubernetes feature/ntp feature/ostree \
    feature/python-tkinter feature/passwd-expire feature/xfce feature/kernel-dev \
    feature/lat \
"

KERNEL_FEATURES:append = " features/nfsd/nfsd-enable.scc"
KERNEL_MODULE_AUTOLOAD += "nfsd"

# The kernel-module-split.bbclass sets it as "-${KERNEL_VERSION}" which makes
# the package name as "kernel-module-<func>-<version>-yocto-standard", this
# name doesn't work for dnf's NEVR comparison during pacakge upgrade, so set it
# as blank.
KERNEL_MODULE_PACKAGE_SUFFIX = ""

SDKMACHINE = "x86_64"

VIRTUAL-RUNTIME_graphical_init_manager = "lxdm"

#### For ostree
GPG_PATH ?= "${TOPDIR}/.gnupg"
OSTREE_USE_AB = "0"

PACKAGE_FEED_URIS ??= "http://localhost:9310/outdir"
# Use the first one of PACKAGE_FEED_URIS as OSTREE_REMOTE_URL
OSTREE_REMOTE_URL = "${@d.getVar('PACKAGE_FEED_URIS').split()[0]}/lts-22/${MACHINE}/repos/ostree_repo"

DISTRO_VERSION:pn-grub-efi = "-"

# Size of the Root partition 4GB
OSTREE_FDISK_RSZ = "4096"

#### Packages repos
INHERIT += "packagefeed-stability"

# Skip comparing if PE, PV and PR are the same since there is a PR server.
PACKAGE_COMPARE_FAST = "1"

PACKAGE_FEED_BASE_PATHS = "lts-22/${MACHINE}/repos/rpm"

PACKAGE_FEED_ARCHS:xilinx-zynq = "armv7ahf_neon armv7at2hf_neon noarch xilinx_zynq"

